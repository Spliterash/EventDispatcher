plugins {
    id 'java-library'
    id "io.freefair.lombok" version "6.6.1"
    id "maven-publish"
}

allprojects {
    apply plugin: "java-library"
    apply plugin: "io.freefair.lombok"
    apply plugin: "maven-publish"

    group 'ru.spliterash'
    version '1.0.0'

    compileJava {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    repositories {
        mavenCentral()
        mavenLocal()

        maven {
            url = "https://repo.spliterash.ru/group/"
        }
    }

    dependencies {
        api 'net.jodah:typetools:0.6.3'
        implementation 'ru.spliterash:spcore-structures-linked-list:1.0.5'


        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
        testImplementation 'org.mockito:mockito-core:4.3.1'
    }

    test {
        useJUnitPlatform()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java

                artifactId = "event-dispatcher" + (project == rootProject ? "" : project.path.replace(":", "-"))
            }
        }
        repositories {
            maven {
                name = "nexus"
                url = "https://repo.spliterash.ru/event-dispatcher"
                credentials {
                    username = project.property("SPLITERASH_NEXUS_USR") as String
                    password = project.property("SPLITERASH_NEXUS_PSW") as String
                }
            }
        }
    }

}
subprojects {
    dependencies {
        api rootProject
    }
}